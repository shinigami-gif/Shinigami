plugins {
    alias(libs.plugins.android)
    alias(libs.plugins.serialization)
    alias(libs.plugins.ksp)
    alias(libs.plugins.metro)
    alias(libs.plugins.sqldelight)
}

if (gradle.startParameter.taskNames.any { it.contains("google", true) }) {
    apply(plugin = "com.google.gms.google-services")
    apply(plugin = "com.google.firebase.crashlytics")
}

val baseVersion = "3.2.2"

fun computeGitCommitHash(): String {
    val envHash = System.getenv("COMMIT_HASH")
    if (!envHash.isNullOrBlank()) {
        return envHash.take(7)
    }
    val gitHash = try {
        providers.exec {
            commandLine("git", "rev-parse", "HEAD")
        }.standardOutput.asText.get().trim().take(7)
    } catch (e: Exception) {
        try {
            providers.exec {
                commandLine("git", "rev-parse", "--verify", "--short=7", "HEAD")
            }.standardOutput.asText.get().trim()
        } catch (e2: Exception) {
            ""
        }
    }
    if (gitHash.isNotEmpty()) {
        return gitHash
    }
    val fallbackHash = System.getenv("GITHUB_SHA")
    if (!fallbackHash.isNullOrBlank()) {
        return fallbackHash.take(7)
    }
    return ""
}

val gitCommitHash = computeGitCommitHash()

android {
    namespace = "ani.dantotsu"
    compileSdk = 37

    defaultConfig {
        applicationId = "ani.shinigami.app"
        minSdk = 26
        targetSdk = 36

        versionName = if (gitCommitHash.isNotEmpty()) "$baseVersion+$gitCommitHash" else baseVersion
        versionCode = baseVersion.split(".")
            //noinspection WrongGradleMethod
            .map { it.toInt() * 100 }
            .joinToString("")
            .toInt()

        signingConfig = signingConfigs.getByName("debug")
    }

    splits {
        abi {
            val singleApk = providers.gradleProperty("singleApk").isPresent
            isEnable = !singleApk

            if (!singleApk) {
                reset()
                val fdroidAbi = providers.gradleProperty("fdroidAbi").orNull
                if (fdroidAbi != null) {
                    include(fdroidAbi)
                } else {
                    include("armeabi-v7a", "arm64-v8a", "x86", "x86_64")
                }
                // F-Droid builds are consumed as per-ABI APKs; Google keeps the universal APK.
                isUniversalApk = !providers.gradleProperty("fdroid").isPresent
            }
        }
    }

    flavorDimensions += "store"

    productFlavors {
        create("fdroid") {
            dimension = "store"
            versionNameSuffix = "-fdroid"
        }
        create("google") {
            dimension = "store"
            isDefault = true
        }
    }

    buildTypes {
        create("alpha") {
            applicationIdSuffix = ".beta"
            versionNameSuffix = "-alpha01"
            manifestPlaceholders["icon_placeholder"] = "@mipmap/ic_launcher_alpha"
            manifestPlaceholders["icon_placeholder_round"] = "@mipmap/ic_launcher_alpha_round"
            isDebuggable = true
            isJniDebuggable = true
            isMinifyEnabled = false
            isShrinkResources = false
            isDefault = true
        }

        getByName("debug") {
            applicationIdSuffix = ".beta"
            versionNameSuffix = "-beta01"
            manifestPlaceholders["icon_placeholder"] = "@mipmap/ic_launcher_beta"
            manifestPlaceholders["icon_placeholder_round"] = "@mipmap/ic_launcher_beta_round"
            isDebuggable = false
        }

        getByName("release") {
            manifestPlaceholders["icon_placeholder"] = "@mipmap/ic_launcher"
            manifestPlaceholders["icon_placeholder_round"] = "@mipmap/ic_launcher_round"
            isDebuggable = false
            isMinifyEnabled = true
            isShrinkResources = true
            proguardFiles(
                getDefaultProguardFile("proguard-android-optimize.txt"),
                "proguard-rules.pro"
            )
        }
    }

    buildFeatures {
        viewBinding = true
        buildConfig = true
        aidl = true
    }

    packaging {
        jniLibs {
            useLegacyPackaging = true
            pickFirsts.add("**/libavcodec.so")
            pickFirsts.add("**/libavdevice.so")
            pickFirsts.add("**/libavfilter.so")
            pickFirsts.add("**/libavformat.so")
            pickFirsts.add("**/libavutil.so")
            pickFirsts.add("**/libswresample.so")
            pickFirsts.add("**/libswscale.so")
        }
    }

    compileOptions {
        isCoreLibraryDesugaringEnabled = true
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }
}

kotlin {
    jvmToolchain(21)
    compilerOptions {
        freeCompilerArgs.addAll(
            "-XXLanguage:+ContextParameters",
            "-Xmulti-platform"
        )
    }
}

configurations.all {
    exclude(group = "org.json", module = "json")
}

dependencies {
    implementation(project(":backend"))
    // ffmpeg-kit (must precede media3 so complete native binaries with av_log_default_callback are chosen by pickFirsts)
    implementation(libs.ffmpeg.kit)

    // Media3 & decoders
    implementation(libs.bundles.media3)
    implementation(libs.bundles.subtitles)
    implementation(libs.mediarouter)
    // HTTP/3 (QUIC) — media3-datasource-cronet:1.11.1 API surface:
    // Tier 1 (GMS devices): CronetDataSource via Play Services CronetProvider — HTTP/3 + HTTP/2.
    // Tier 2 (fallback / F-Droid): OkHttp — HTTP/2. CronetProvider absent → caught → falls through.
    implementation(libs.media3.cronet)
    // GMS Cronet provider — google flavor only; absent from F-Droid APK
    add("googleImplementation", libs.play.services.cronet)

    // Firebase
    add("googleImplementation", platform(libs.firebase.bom))
    add("googleImplementation", libs.bundles.firebase)

    // AndroidX
    implementation(libs.bundles.androidx)
    implementation(libs.androidx.webkit)

    // Kotlin
    implementation(libs.kotlin.reflect)
    implementation(libs.kotlin.stdlib)

    // Core libs
    implementation(libs.bundles.misc)
    implementation(libs.metro.runtime)
    implementation(libs.bundles.sqldelight)
    implementation(libs.androidx.sqlite.bundled)
    implementation(libs.androidx.profileInstaller)

    // Shizuku
    implementation(libs.bundles.shizuku)

    // Glide
    implementation(libs.bundles.glide)
    ksp(libs.glide.ksp)

    coreLibraryDesugaring(libs.desugar.jdk.libs)

    // UI
    implementation(libs.material)
    implementation(libs.materialKolor)
    implementation(files("libs/AnimatedBottomBar-7fcb9af.aar"))
    implementation(libs.flexbox)
    implementation(libs.kenburns)
    implementation(libs.subsampling)
    implementation(libs.gesture)
    implementation(libs.play.services.base)
    implementation(libs.dialogs)
    implementation(libs.charts)

    implementation(libs.bundles.markwon)
    implementation(libs.bundles.groupie)
    implementation(libs.bundles.rx)
    implementation(libs.bundles.okhttp)
    implementation(libs.okio)



    // LeakCanary & Plumber (Active in Debug, Alpha, and Release builds for memory leak diagnosis)
    debugImplementation(libs.leakcanary.android)
    debugImplementation(libs.leakcanary.plumber)
}
    
sqldelight {
    databases {
        create("ShinigamiDatabase") {
            packageName.set("ani.dantotsu.database")
            generateAsync.set(true)
        }
    }
}
